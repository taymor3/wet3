	%{
	    #include "part3_helper.hpp"
	    using namespace std;
	    extern "C" int yylex();
		extern int yylineno;
		extern char* yytext;
		extern void yylex_destroy();
	    int yyerror(char const* s);
	    int printOperationalError (string err);
	    int printSemanticError(string err);
	%}

	%token TINT8
	%token TINT16
	%token TINT32
	%token TVOID
	%token TWRITE
	%token TREAD
	%token TWHILE
	%token TDO
	%token TRET
	%token TID
	%token TNUM
	%token  TSTR
	%right TASSIGN
	%right TIF TELSE TTHEN
	%left TOR 
	%left TAND
	%left TRELOP
	%left TADDOP
	%left TMULOP
	%right TNOT
	%left ','
	%left '('
	%left ')'
	%left '{'
	%left '}'
	%left ':'
	%left ';'


%%
	PROGRAM : FDEFS {
		// After all the functions in the file are defined, we know their starting address so we fill
		// the holes of JLINK in the code
		for(map<string, Function>::iterator it = functionTable.begin(); it != functionTable.end(); it++) {
			buffer->backpatch(it->second.callingLines, it->second.startLineImplementation);
		}
	}
	;


	FDEFS : FDEFS FUNC_API BLK M {
		// This semantic rule indicates a function is being defined

		// Check if the function has already been defined in this .cmm file before
		if(functionTable[$2.str].defined != false) {
			printSemanticError("redefinition of function '" + $2.str + "'");
		}
		else { 
			functionTable[$2.str].defined = true; // Indicate that a function with $2.str name is defined
		}
		buffer->emit("RETRN");

		// Initialize the relevant globals 
		currentScopeRegsNum = 3;
		currentScopeOffset = 0;
		symbolTable.clear();
		$2.paramTypes.clear();
	}
	| FDEFS FUNC_API ';' {
		// This semantic rule indicates a function is being declared
		
		// Only if the function has'nt been defined yet, we assume it's definition exists in an outer file
		if (functionTable[$2.str].defined != true) {
			functionTable[$2.str].startLineImplementation = -1;
		}
		symbolTable.clear();
	}
	| /* EPSILON */ { }
	;


	FUNC_API : TYPE TID '(' M FUNC_ARGS ')' {

		// Check if the function is main its return type
		if ($2.str == "main" && $1.type != void_t) {
			printSemanticError("incompatible return type of main function");
		}

		// Check if the function already exists in the function table
		if (functionTable.find($2.str) != functionTable.end()) { 
			//function is already in the function table.

			// In case a function had already been declared and now being implemented, do some checkups:

			// Check that the number of parameters match
			if ($5.paramTypes.size() != functionTable[$2.str].paramTypes.size()) {
				printSemanticError("conflicting number of arguments for '" + $2.str + "' function");
			}
			// Check arguments type compatibility
			for (int i = 0; i < functionTable[$2.str].paramTypes.size(); i++) {
				if (functionTable[$2.str].paramTypes[i] != $5.paramTypes[i]) {
					printSemanticError("conflicting types for '" + $2.str + "' function");
				}
			}
			// Compare return type of declared function to return type of currently defined function
			if ($1.type != functionTable[$2.str].returnType) {
				printSemanticError("conflicting return type for '" + $2.str + "' function");
			}

			// Only if the function has'nt been defined yet, we assume that it is currently being defined and 
			// set its starting implemintation line in the .rsk file to be the next available address in buffer
			if (functionTable[$2.str].defined != true) {
				functionTable[$2.str].startLineImplementation = $4.quad;
			}
		}
		else {
			Function newFunction;
			newFunction.returnType = $1.type;
			newFunction.startLineImplementation = $4.quad;
			newFunction.paramTypes = $5.paramTypes;
			newFunction.defined = false;

			// Insert to function table
			functionTable.insert(std::pair<string,Function>($2.str, newFunction));
		}
		currentReturnType = $1.type;
		$$.str = $2.str;
		$$.type = $1.type;
		$$.quad = $4.quad;
		$$.paramTypes = $5.paramTypes;
		$5.paramTypes.clear();
	}
	;


	FUNC_ARGS : FUNC_ARGLIST {
		// Here we set the offset and type of each parameter and pass it up to FUNC_ARGS 

		// Offset from I1 register in the reverse direction. We save 4 bytes for function return value
		int offset = -4;

		vector<Type> paramTypesVec;
	
		for (int i = 0; i < currentParamInsertionOrder.size(); i++) {
			string paramName = currentParamInsertionOrder[i];
			Type paramType = symbolTable[paramName].type[0];
			paramTypesVec.push_back(paramType);

			if(paramType == int8) {
				offset--;
			}
			else if (paramType == int16) {
				if (offset%2 == 0) {
					offset -=2;
				}
				else {
					offset -= 3;
				}
			}
			else if (paramType == int32) {
				if (offset%4 == 0) {
					offset -= 4;
				}
				else {
					offset = offset - (offset%4) - 4;
				}
			}
			symbolTable[paramName].offset[0] = offset;
		}

		$$.paramTypes = paramTypesVec;
		currentParamInsertionOrder.clear();
		currentScopeOffset = currentScopeOffset + offset + 4;
	}
	| /* EPSILON */ { }
	;


	FUNC_ARGLIST : FUNC_ARGLIST ',' DCL {
		// Insert the function's parameters to a global list in the correct order
		for(int i=tmpParamInsertionOrder.size()-1; i>=0; i--) {
			string paramName = tmpParamInsertionOrder[i];
			currentParamInsertionOrder.push_back(paramName);
		}
		tmpParamInsertionOrder.clear();
	}
	| DCL {
		for(int i=tmpParamInsertionOrder.size()-1; i>=0; i--) {
			string paramName = tmpParamInsertionOrder[i];
			currentParamInsertionOrder.push_back(paramName);
		}
		tmpParamInsertionOrder.clear();
	}
	;
  

	BLK : '{' BLK_OPEN STLIST M BLK_CLOSE '}' {
		buffer->backpatch($3.nextList, $4.quad);
	}
	;

	BLK_OPEN : /* EPSILON */ {
		// Entering a deeper block. Increase the global depth counter
		currentBlockDepth++;
	}
	;


	BLK_CLOSE : /* EPSILON */ {
		
		// Delete all the vars declared in the current scope when ending the current scope
		for(std::map<string, Symbol>::iterator it = symbolTable.begin(); it != symbolTable.end(); ++it){
			if (it->second.depth == currentBlockDepth) {
				it->second.type.erase(currentBlockDepth);
				it->second.offset.erase(currentBlockDepth);
				it->second.depth--;
			}
		}
		// Exiting a block. Decrease the global depth counter
		currentBlockDepth--;
	}


	DCL : TID ':' TYPE {

		if ($3.type == void_t) {
			printSemanticError("variable has incompatible type 'void'");
		}

		// Check that an id with the same name does not exist in the currrent function list parameters
		if (find(currentParamInsertionOrder.begin(), currentParamInsertionOrder.end(), $1.str) != currentParamInsertionOrder.end()) {
			printSemanticError("redefinition of parameter '" + $1.str + "'");
		}

		prevScopeOffset = currentScopeOffset;
		$$.str = $1.str;
		$$.type = $3.type;
		$$.offset = currentScopeOffset;

		if( symbolTable.find($1.str) == symbolTable.end()) { 
			// The symbol $1.str is not defined yet

			Symbol s;
			s.type[currentBlockDepth] = $3.type;
			s.depth = currentBlockDepth;

			// Increase current scope offset according to type
			if ($3.type == int8) {
				s.offset[currentBlockDepth] = currentScopeOffset;
				currentScopeOffset += 1;
			}
			else if ($3.type == int16) {
				if (currentScopeOffset%2 == 0) {
					s.offset[currentBlockDepth] = currentScopeOffset;
					
					currentScopeOffset += 2;
				}
				else { 
					// If global (offset mod 4)=1 or (offset mod 4)=3 then skip one byte and use the following two bytes
					s.offset[currentBlockDepth] = currentScopeOffset + 1;
					currentScopeOffset += 3;
				}
			}
			else if ($3.type == int32) {
				if (currentScopeOffset%4 == 0) {
					s.offset[currentBlockDepth] = currentScopeOffset;
					currentScopeOffset += 4;
				}
				else {
					s.offset[currentBlockDepth] = currentScopeOffset + 4 - (currentScopeOffset%4);
					currentScopeOffset += (4 - (currentScopeOffset%4)) + 4;
				}
			}

			symbolTable.insert(std::pair<string, Symbol>($1.str, s));
			tmpParamInsertionOrder.push_back($1.str);
		}
		else { 
			// Identifier with this name (str) already exists. we need to check if it is in the same block level or not.
			
			// ERROR: a new id can't be defined with the same name as an existing id in the same level
			if(symbolTable[$1.str].type.size() == currentBlockDepth) {
				printSemanticError("repeated declaration of identifier '" + $1.str + "'");
			}
			// Add another id in the current block depth
			else {
				symbolTable[$1.str].depth = currentBlockDepth;
				symbolTable[$1.str].type[currentBlockDepth] = $3.type;
				if ($3.type == int8) {
					symbolTable[$1.str].offset[currentBlockDepth] = currentScopeOffset;
					currentScopeOffset += 1;
				}
				else if ($3.type == int16) {
					if (currentScopeOffset%2 == 0) {
						symbolTable[$1.str].offset[currentBlockDepth] = currentScopeOffset;
						currentScopeOffset += 2;
					}
					else { 
						// If global (offset mod 4)=1 or (offset mod 4)=3 then skip one byte and use the following two bytes.
						symbolTable[$1.str].offset[currentBlockDepth] = currentScopeOffset+1;
						currentScopeOffset += 3;
					}
				}
				else if ($3.type == int32) {
					if (currentScopeOffset%4 == 0) {
						symbolTable[$1.str].offset[currentBlockDepth] = currentScopeOffset;
						currentScopeOffset += 4;
					}
					else {
						symbolTable[$1.str].offset[currentBlockDepth] = currentScopeOffset + 4 - (currentScopeOffset%4);
						currentScopeOffset += (4 - (currentScopeOffset%4)) + 4;
					}
				}
			}
		}
	}
	| TID ',' DCL {
		if ($3.type == void_t) {
			printSemanticError("variable has incompatible type 'void'");
		}

		// Check in both lists that an id with the same name does not exist in the currrent function list parameters
		if (find(tmpParamInsertionOrder.begin(), tmpParamInsertionOrder.end(), $1.str) != tmpParamInsertionOrder.end()) {
			printSemanticError("repeated definition of parameter '" + $1.str + "'");
		}
		if (find(currentParamInsertionOrder.begin(), currentParamInsertionOrder.end(), $1.str) != currentParamInsertionOrder.end()) {
			printSemanticError("repeated definition of parameter '" + $1.str + "'");
		}

		$$.str = $1.str;
		$$.type = $3.type;

		if( symbolTable.find($1.str) == symbolTable.end()) { 
			// The symbol is not defined yet

			Symbol s;
			s.type[currentBlockDepth] = $3.type;
			s.depth = currentBlockDepth;

			// Increase current scope offset acording to type
			if ($3.type == int8) {
				s.offset[currentBlockDepth] = currentScopeOffset;
				currentScopeOffset += 1;
			}
			else if ($3.type == int16) {
				if (currentScopeOffset%2 == 0) {
					s.offset[currentBlockDepth] = currentScopeOffset;
					
					currentScopeOffset += 2;
				}
				else { 
					// If global (offset mod 4)=1 or (offset mod 4)=3 then skip one byte and use the following two bytes
					s.offset[currentBlockDepth] = currentScopeOffset + 1;
					currentScopeOffset += 3;
				}
			}
			else if ($3.type == int32) {
				if (currentScopeOffset%4 == 0) {
					s.offset[currentBlockDepth] = currentScopeOffset;
					currentScopeOffset += 4;
				}
				else {
					s.offset[currentBlockDepth] = currentScopeOffset + 4 - (currentScopeOffset%4);
					currentScopeOffset += (4 - (currentScopeOffset%4)) + 4;
				}
			}

			symbolTable.insert(std::pair<string, Symbol>($1.str, s));
			tmpParamInsertionOrder.push_back($1.str);
		}
		else { 
			// Identifier with this name (str) already exists. we need to check if it is in the same block level or not.
			
			// ERROR: a new id can't be defined with the same name as an existing id in the same level
			if(symbolTable[$1.str].type.size() == currentBlockDepth) {
				printSemanticError("repeated declaration of identifier '" + $1.str + "'");
			}
			//Add another identifier in the current block depth
			else {
				symbolTable[$1.str].depth = currentBlockDepth;
				symbolTable[$1.str].type[currentBlockDepth] = $3.type;
				if ($3.type == int8) {
					symbolTable[$1.str].offset[currentBlockDepth] = currentScopeOffset;
 
					currentScopeOffset += 1;
				}
				else if ($3.type == int16) {
					if (currentScopeOffset%2 == 0) {
						symbolTable[$1.str].offset[currentBlockDepth] = currentScopeOffset;
						currentScopeOffset += 2;
					}
					else { 
						// If global (offset mod 4)=1 or (offset mod 4)=3 then skip one byte and use the following two bytes
						symbolTable[$1.str].offset[currentBlockDepth] = currentScopeOffset+1;
						currentScopeOffset += 3;
					}
				}
				else if ($3.type == int32) {
					if (currentScopeOffset%4 == 0) {
						symbolTable[$1.str].offset[currentBlockDepth] = currentScopeOffset;
						currentScopeOffset += 4;
					}
					else {
						symbolTable[$1.str].offset[currentBlockDepth] = currentScopeOffset + 4 - (currentScopeOffset%4);
						currentScopeOffset += (4 - (currentScopeOffset%4)) + 4;
					}
				}
			}
		}
	}


	TYPE : TINT8 {
		$$.type = int8;
	}
	| TINT16 {
		$$.type = int16;
	}
	| TINT32 {
		$$.type = int32;
	}
	| TVOID {
		$$.type = void_t;
	}
	;


	STLIST : STLIST STMT {}
	| /* EPSILON */ {}
	;


	STMT : DCL ';' {
		// Increase stack pointer size according to the new offset compared to prevScopeOffset
		tmpParamInsertionOrder.clear();
		currentParamInsertionOrder.clear();
		int offsetInBytes = currentScopeOffset - prevScopeOffset;
		buffer->emit("ADD2I I2 I2 " + intToString(offsetInBytes));
	}
	| ASSN {}
	| EXP ';' {
		if ($1.type != void_t) {
			printSemanticError("expression has incompatible type");
		}
	}
	| CNTRL M {
		// Backpatch control statments' true and false lists
		buffer->backpatch($1.nextList, $2.quad);
	}
	| READ {}
	| WRITE {}
	| RETURN {}
	| BLK {}
	;


	RETURN : TRET EXP ';' {
		if (currentReturnType != $2.type) {
			printSemanticError("incompatible return type");
		}
		else if(currentReturnType == void_t){
			printSemanticError("incompatible return type");
		}
		$$.regNum = $2.regNum;
		int returnValueSize = $2.type / 8;
		buffer->emit("STI" + intToString($2.type) + " I" + intToString($$.regNum) + " I1 -" + intToString(returnValueSize));
		buffer->emit("RETRN");
	}
	| TRET ';' { 
		buffer->emit("LDI32 I0 I2 4");
		if (currentReturnType != void_t) {
			printSemanticError("function return value is not void");
		}
		buffer->emit("RETRN");
	}
	;


	WRITE : TWRITE '(' EXP ')' ';' {
		if ($3.type == int32) {
			buffer->emit("PRNTI I" + intToString($3.regNum));
		}
		else {
			// Handle conversion to int32
			$$.regNum = currentScopeRegsNum++;
			int prevReg = $$.regNum - 1; 
			int shiftSize = 32 - $3.type; 
			buffer->emit("SLAI I" + intToString($$.regNum) + " I" + intToString(prevReg) + " " + intToString(shiftSize));
			buffer->emit("SRAI I" + intToString($$.regNum) + " I" + intToString($$.regNum) + " " + intToString(shiftSize));
			buffer->emit("PRNTI I" + intToString($$.regNum));
		}  
	}
	| TWRITE '(' TSTR ')' ';' {
		for (unsigned int i = 0 ; i < $3.str.length() ; i++) {
			char c = $3.str[i];
			if (c == '\\') {
				if ($3.str[i+1] == 'n') {
					c = '\n';
					i++;
				}
				else if ($3.str[i+1] == 't') {
					c = '\t';
					i++;
				}
			}
			int val = static_cast<int>(c);
			buffer->emit("PRNTC " + intToString(val));
		}
	}
	;


	READ : TREAD '(' LVAL ')' ';' {
		int tempRegister = currentScopeRegsNum++;
		buffer->emit("READI I" + intToString(tempRegister));
		buffer->emit("STI" + intToString($3.type) + " I" + intToString(tempRegister) + " I" + intToString($3.regNum) + " 0");
	}
	;


	ASSN : LVAL TASSIGN EXP ';' {
		Type lvalType = $1.type;
		Type rvalType = $3.type;

		// Check if both arguments are of the same type
		if ($1.type != $3.type) {
			printSemanticError("incompatible type of argument '" + $3.str + "'");
		}
		else if ($1.type == void_t) {
			printSemanticError("can't perform this operation on void type arguments");
		}
		buffer->emit("STI" + intToString($1.type) + " I" + intToString($3.regNum) + " I" + intToString($1.regNum) + " 0");
	}
	;


	LVAL : TID {
		// Check that variable is declared
		if (symbolTable.find($1.str) == symbolTable.end()) {
			printSemanticError("use of undeclared identifier '" + $1.str + "'");
		}
		int depth = symbolTable[$1.str].depth;
		$$.type = symbolTable[$1.str].type[depth];
		
		if ($$.type == void_t) {
			printSemanticError("variable '" + $1.str + "' has incompatible type");
		}

		// Allocate a register for the memory offset calulation
		$$.regNum = currentScopeRegsNum++; 
		$$.offset = symbolTable[$1.str].offset[depth];
		buffer->emit("ADD2I I" + intToString($$.regNum) + " I1 " + intToString($$.offset));
	}
	;


	CNTRL : TIF BEXP TTHEN M STMT TELSE N M STMT {
		buffer->backpatch($2.trueList, $4.quad);
		buffer->backpatch($2.falseList, $8.quad);
		$$.nextList = merge<int>($5.nextList, $7.nextList);
		$$.nextList = merge<int>($$.nextList, $9.nextList);
		$5.nextList.clear();
		$9.nextList.clear();
		$7.nextList.clear();
	}
	| TIF BEXP TTHEN M STMT {
		buffer->backpatch($2.trueList, $4.quad);
		$$.nextList = merge<int>($2.falseList, $5.nextList);
		$2.falseList.clear();
		$5.nextList.clear();
	}
	| TWHILE M BEXP TDO M STMT {
		buffer->backpatch($3.trueList, $5.quad);
		buffer->backpatch($6.nextList, $2.quad);
		$$.nextList = $3.falseList;
		// Emit to buffer UJUMP command. After backpatch the addressed would be filled
		buffer->emit("UJUMP " + intToString($2.quad));
	}
	;


	BEXP : BEXP TOR M BEXP {
		buffer->backpatch($1.falseList, $3.quad);
		$$.falseList = $4.falseList;
		$$.trueList = merge<int>($1.trueList, $4.trueList);
		$1.trueList.clear();
		$4.trueList.clear();
	}
	| BEXP TAND M BEXP {
		buffer->backpatch($1.trueList, $3.quad);
		$$.trueList = $4.trueList;
		$$.falseList = merge<int>($1.falseList, $4.falseList);
		$1.falseList.clear();
		$4.falseList.clear();
	}
	| TNOT BEXP {
		$$.trueList = $2.falseList;
		$$.falseList = $2.trueList;
	}
	| EXP TRELOP EXP {
		int leftRegister = $1.regNum;
		int rightRegister = $3.regNum;

		// Check if both arguments are of the same type
		if ($1.type != $3.type) {
			printSemanticError("incompatible type of arguments");
		}
		else if ($1.type == void_t) {
			printSemanticError("can't perform this operation on 'void' type arguments");
		}
		$$.regNum = currentScopeRegsNum++;

		// Set UJUMP for backpatch
		$$.falseList.push_back(buffer->nextquad() + 1);
		// Set BNEQZ for backpatch 
		$$.trueList.push_back(buffer->nextquad() + 2);  
		
		if ($2.str == "==") {
			buffer->emit("SEQUI I" + intToString($$.regNum) + " I" + intToString(leftRegister) + " I" + intToString(rightRegister));
			buffer->emit("BREQZ I" + intToString($$.regNum) + " ");
		} 
		else if ($2.str== "<>") {
			buffer->emit("SNEQI I" + intToString($$.regNum) + " I" + intToString(leftRegister) + " I" + intToString(rightRegister));
			buffer->emit("BREQZ I" + intToString($$.regNum) + " ");
		} 
		else if ($2.str== "<=") {
			buffer->emit("SGRTI I" + intToString($$.regNum) + " I" + intToString(leftRegister) + " I" + intToString(rightRegister));
			buffer->emit("BNEQZ I" + intToString($$.regNum) + " ");
		} 
		else if ($2.str== "<") {
			buffer->emit("SLETI I" + intToString($$.regNum) + " I" + intToString(leftRegister) + " I" + intToString(rightRegister));
			buffer->emit("BREQZ I" + intToString($$.regNum) + " ");
		} 
		else if ($2.str== ">=") {
			buffer->emit("SLETI I" + intToString($$.regNum) + " I" + intToString(leftRegister) + " I" + intToString(rightRegister));
			buffer->emit("BNEQZ I" + intToString($$.regNum) + " ");
		} 
		else if ($2.str== ">") {
			buffer->emit("SGRTI I" + intToString($$.regNum) + " I" + intToString(leftRegister) + " I" + intToString(rightRegister));
			buffer->emit("BREQZ I" + intToString($$.regNum) + " ");
		}
		buffer->emit("UJUMP ");
	}
	| '(' BEXP ')' {
		$$.trueList = $2.trueList;
		$$.falseList = $2.falseList;
	}
	;
 


	EXP : EXP TADDOP EXP {

		int leftRegister = $1.regNum;
		int rightRegister = $3.regNum;

		if ($1.type == void_t || $3.type == void_t) {
			printSemanticError("expression has incompatible type 'void'");
		}

		// Check if both arguments are of the same type
		else if ($1.type != $3.type) {
			printSemanticError("incompatible type of arguments");
		}
		
		$$.type = $1.type;
		$$.regNum = currentScopeRegsNum++;

		if ($2.str== "+") {
			buffer->emit("ADD2I I" + intToString($$.regNum) + " I" + intToString(leftRegister) + " I" + intToString(rightRegister));
		}
		else {
			buffer->emit("SUBTI I" + intToString($$.regNum) + " I" + intToString(leftRegister) + " I" + intToString(rightRegister));
		}

	}
	| EXP TMULOP EXP {

		int leftRegister = $1.regNum;
		int rightRegister = $3.regNum;

		// Check if both arguments are of the same type
		if ($1.type != $3.type) {
			printSemanticError("incompatible type of arguments");
		}
		else if ($1.type == void_t) {
			printSemanticError("can't perform this operation on 'void' type argument");
		}
		$$.type = $1.type;
		$$.regNum = currentScopeRegsNum++;

		if ($2.str== "*") {
			buffer->emit("MULTI I" + intToString($$.regNum) + " I" + intToString(leftRegister) + " I" + intToString(rightRegister));
		}
		else {
			buffer->emit("DIVDI I" + intToString($$.regNum) + " I" + intToString(leftRegister) + " I" + intToString(rightRegister));
		}
	}
	| '(' EXP ')' {
		$$ = $2;
	}
	| '(' TYPE ')' EXP {
		if ($2.type != void_t && $4.type == void_t) {
			printSemanticError("can't perform this operation on 'void' type argument");
		}

		if ($2.type == $4.type) {
			$$ = $4;
		}
		else if ($2.type > $4.type) {
			$$.regNum = currentScopeRegsNum++;
			$$.type = $2.type;
			$$.offset = $4.offset;
			int prevReg = $$.regNum - 1;
			int shiftSize =  32 - $4.type;			
			buffer->emit("SLAI I" + intToString($$.regNum) + " I" + intToString(prevReg) + " " + intToString(shiftSize));
			buffer->emit("SRAI I" + intToString($$.regNum) + " I" + intToString($$.regNum) + " " + intToString(shiftSize));

			if($2.type == int16) {
				// Decimal 65535 is equal to 16 bits of '1'
				buffer->emit("ANDI I" + intToString($$.regNum) + " I" + intToString($$.regNum) + " 65535"); 
			}
		}
		else if ($4.type > $2.type) {
			$$.regNum = currentScopeRegsNum++;
			$$.type = $2.type;
			$$.offset = $4.offset;
			int prevReg = $$.regNum - 1;
			if($2.type == int16){
				buffer->emit("ANDI I" + intToString($$.regNum) + " I" + intToString(prevReg) + " 65535");
			}
			else if ($2.type == int8) {
				buffer->emit("ANDI I" + intToString($$.regNum) + " I" + intToString(prevReg) + " 255");
			}
		}
	}
	| TID {
		// Check that variable is declared
		if (symbolTable.find($1.str) == symbolTable.end()) {
			printSemanticError("use of undeclared identifier '" + $1.str + "'");
		}
		int depth = symbolTable[$1.str].depth;
		$$.type = symbolTable[$1.str].type[depth];
		$$.regNum = currentScopeRegsNum++;
		$$.offset = symbolTable[$1.str].offset[depth];
		
		buffer->emit("LDI" + intToString($$.type) + " I" + intToString($$.regNum) + " I1 " + intToString($$.offset));
	}
	| TNUM {
		$$.type = int32;
		$$.regNum = currentScopeRegsNum++;
		$$.str = $1.str;
		buffer->emit("COPYI I" + intToString($$.regNum) + " " + $1.str);
	}
	| CALL {
		$$.regNum = $1.regNum;
		$$.type = $1.type;
	}
	;
 


	CALL : TID '(' CALL_ARGS ')' {

		string funcName = $1.str;

		// Check if function name was declared
		if (functionTable.find(funcName) == functionTable.end()) {
			printSemanticError("use of unknown function '" + funcName + "'");
		}

		// Check if number of arguments is correct
		if (functionTable[funcName].paramTypes.size() != $3.paramTypes.size()) {
			printSemanticError("incorrect number of arguments of function '" + funcName + "'");
		}

		//check arguments type compatibility
		for (int i = 0; i < functionTable[funcName].paramTypes.size(); i++) {
			if (functionTable[funcName].paramTypes[i] != $3.paramTypes[i]) {
				printSemanticError("incompatible type of argument in function '" + funcName + "'");
			}
		}
		
		int shiftSize = 0;
		//Allign offset to memory line (4 bytes)
		if (currentScopeOffset%4 != 0) {
			shiftSize = 4 - (currentScopeOffset%4);
			currentScopeOffset += shiftSize;
			buffer->emit("ADD2I I2 I2 " + intToString(shiftSize));
		}
		// Save all registers to memory
		int offset = 0;

		for (int i = 0; i < currentScopeRegsNum; ++i) {
			buffer->emit("STI32 I" + intToString(i) + " I2 " + intToString(offset));
			offset += 4;
		}

		// Add 4 bytes for storing called function return value
		offset += 4;

		int paramsCurrentOffset = -4;
		int paramCount = $3.paramRegs.size();
		vector<Type> paramTypes = $3.paramTypes;
		vector<int> paramRegs = $3.paramRegs;
		vector<int> paramOffset;

		$3.paramRegs.clear();
		$3.paramTypes.clear();

		// Calculate the offset in memory to store each parameter of the called function
		for (int i = 0; i < paramCount; i++) {
			if (paramTypes[i] == int8) {
				offset += 1;
				paramsCurrentOffset--;
			}
			else if(paramTypes[i] == int16) {
				if (offset%2 == 0) {
						offset += 2;
						paramsCurrentOffset -=2;
					}
					else { 
						// If global (offset mod 4)=1 or (offset mod 4)=3 then skip one byte and use the following two bytes
						offset += 3;
						paramsCurrentOffset -=3;
					}
				}
			else if (paramTypes[i] == int32) {
				if (offset%4 == 0) {
					offset += 4;
					paramsCurrentOffset -=4;
				}
				else {
					offset += (4 - (offset%4)) + 4;
					paramsCurrentOffset = paramsCurrentOffset - (offset%4) - 4;
				}
			}
			paramOffset.push_back(paramsCurrentOffset);
		}
		// Allign offset to memory line (4 bytes)
		if (offset%4 != 0) {
			offset = offset + 4 - (offset%4);
		}
		// Update I2 to the current memory size. update global offset
		buffer->emit("ADD2I I2 I2 " + intToString(offset));
		currentScopeOffset += offset;

		// Set I1 = I2 
		buffer->emit("COPYI I1 I2");
	
		// Store calling function parameters in stack according to each parameter's offset in memory
		for (int i = 0; i < paramCount; i++) {
			buffer->emit("STI" + intToString(paramTypes[i]) + " I" + intToString(paramRegs[i]) + " I1 " + intToString(paramOffset[i]));
		}
		
		// Execute JUMP and LINK

		// Add the current line number to the calling lines list of the current function
		functionTable[funcName].callingLines.push_back(buffer->nextquad());
		
		buffer->emit("JLINK "); 
		
		// Back from function, set the stack frame I2 = I1
		buffer->emit("COPYI I2 I1");
		
		// Set the return value to a register
		$$.type = functionTable[funcName].returnType;
		$$.regNum = currentScopeRegsNum;

		// Load from memory the called function's return value, in case return type isn't void
		int returnValueSize = functionTable[funcName].returnType;
		if (returnValueSize != 0) {
			buffer->emit("LDI" + intToString(returnValueSize) + " I" + intToString($$.regNum) + " I1 -" + intToString(returnValueSize/8));
		}
		buffer->emit("SUBTI I2 I2 " + intToString(offset));
		
		// Restore all registers from memory
		offset = 0;
		
		for (int i = 0 ; i < currentScopeRegsNum ; i++) {
			if (i == 2) {
				offset += 4;
				continue;
			}
			buffer->emit("LDI32 I" + intToString(i) + " I2 " + intToString(offset));
			offset += 4;
		}

		// If we increased I2 in order to allign SP to memory, we need to set it back
		if (shiftSize != 0) {
			currentScopeOffset -= shiftSize;
			buffer->emit("SUBTI I2 I2 " + intToString(shiftSize));
		}
		currentScopeRegsNum++;	
	}
	;


	CALL_ARGS : CALL_ARGLIST {
		$$.paramTypes = $1.paramTypes;	
		$$.paramRegs = $1.paramRegs;
		$1.paramTypes.clear();
		$1.paramRegs.clear();
	}
	| /* EPSILON */ {}
	;


	CALL_ARGLIST : CALL_ARGLIST ',' EXP {
		$$.paramRegs = $1.paramRegs;
		$$.paramTypes = $1.paramTypes;
		$$.paramRegs.push_back($3.regNum);
		$$.paramTypes.push_back($3.type);
		$1.paramTypes.clear();
		$1.paramRegs.clear();
		
	}
	| EXP {
		$$.paramTypes.push_back($1.type);
		$$.paramRegs.push_back($1.regNum);
	}
 	;
 

	M : /* EPSILON */ {
		$$.quad = buffer->nextquad();
	}
	;
 

	N : /* EPSILON */ {
		$$.nextList.push_back(buffer->nextquad());
		buffer->emit("UJUMP ");
	}
	;

%%	

int main(int argc, char* argv[])
{
	if (argc != 2) {
		printOperationalError("invalid number of arguments");
	}
	string inputFileName = argv[1];

	extern FILE *yyin;
	// Open the input file 
	yyin = fopen(argv[1], "r");
	if (yyin == NULL) {
		printOperationalError("cannot open input file");
	}
	size_t lastindex;

	lastindex = inputFileName.find_last_of(".");
	if (inputFileName.substr(lastindex) != ".cmm") {
		printOperationalError("invalid file type. expecting '.cmm' extension");
	}
	
	buffer = &mainBuffer;

    yyparse();

	mainBuffer.emit_front("</header>");

	string imp = "<implemented>";
	string uimp = "<unimplemented>";

	for(map<string, Function>::iterator it = functionTable.begin(); it != functionTable.end(); it++) {
		if (it->second.defined > 0) {
			imp += " " + it->first + "," + intToString(it->second.startLineImplementation);
		}
		else {
			uimp += " " + it->first;
			for (int i = 0; i < it->second.callingLines.size(); i++) {
				uimp += "," + intToString(it->second.callingLines[i]);
			}
		}
	}

	mainBuffer.emit_front(imp);
	mainBuffer.emit_front(uimp);

	mainBuffer.emit_front("<header>");

	ofstream rskFile;
	string outputFileName;
	outputFileName = inputFileName.substr(0, lastindex) + ".rsk";
	size_t found = outputFileName.find_last_of("/\\");
	outputFileName = outputFileName.substr(found+1);

	rskFile.open(outputFileName.c_str());

	rskFile << mainBuffer.printBuffer();
	rskFile.close();

	yylex_destroy();
	return 0;
}

int printOperationalError (string err) {
	cerr << "Operational error: " << err << endl;
	exit(OPERATIONAL_ERROR);
}

int printSemanticError (string err) {
	cerr << "Semantic error: " << err << " in line number " << yylineno << endl;
	exit(SEMANTIC_ERROR);
}

int yyerror(char const* s) {
	cerr << "Syntax error: '" << yytext << "' in line number " << yylineno << endl;
	exit(SYNTAX_ERROR);
}